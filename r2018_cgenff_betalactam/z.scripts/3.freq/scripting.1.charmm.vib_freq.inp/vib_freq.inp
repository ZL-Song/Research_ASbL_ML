* PROJ Force Field Dev - small drug molecules
* Molecular vibration frequency optimization
* Basing on Gaussian MP2/6-31Gd virbational spectrum to generate QM target data
* Usage: "important output file name"
*   CHARMM < vib_freq.inp > log.mm
* Zilin Song, 18 JAN 2018
* 

 IOFOrmat EXTEented
 BOMLev -1
 FASTer 1
 PRNLev 0

! { scripting logic control }
 
 SET residue FRG5
 
 SET stage 4        ! 4 = hoh interac calculation basing on qm geometry.
                    ! 5 = ... ....... ........... ...... .. mm .........
 SET crdname fileName

 SET optff ./frg5.strm           ! .strm to optimize.

 ! cgenff force field directory
 ! must have following files: 
 ! par_all36_cgenff.prm / top_all36_cgenff.rtf
 ! toppar_dum_noble_gases.str / toppar_water_ions.str
 SET topparDir ./forcefield

! { Initialize work directory & else }
 
 IF @stage .eq. 4 THEN
  SET crdname @residue.hfmini.crd   ! @residue_hfmini.crd: name of hf optimized coor file.
 ELSE
  SET crdname @residue.mdmini.crd   ! @residue_mdmini.crd: name of mm optimized coor file.
 ENDIf
 

! { read force field Cgenff force fields to support generated force field }

 OPEN UNIT 10 READ CARD NAME @topparDir/top_all36_cgenff.rtf
  READ rtf CARD UNIT 10
  CLOSe UNIT 10
  
 BOMLev -2      ! required for passing NBFIX error.
                ! NBFIX error pops up when:
                ! No protein force field read before cgenff.
 OPEN UNIT 10 READ CARD NAME @topparDir/par_all36_cgenff.prm
  READ param CARD FLEX UNIT 10                ! FLEX is required for mass changed force fields.
  CLOSe UNIT 10
 BOMLev 0
 
 STREam @topparDir/toppar_dum_noble_gases.str    ! dummy atom force field
 STREAM @topparDir/toppar_water_ions.str         ! water tip3 force field

 ! read optimized force field
 STREam @optff

! { target molecule psf & crd }

 READ SEQUence CARD
 * @residue
 * 
 1
 @residue
    
 GENErate @residue FIRSt none LAST none WARN SETUp
    
 OPEN UNIT 20 READ FORM NAME @crdname
  READ coor CARD UNIT 20 APPEnd
  CLOSE UNIT 20

! { do minimization }

 ! MINI CONJ NSTEp 200 NPRINt 20 INBFrq 1000 CUTNb 999
 ! MINI NRAP NSTEp 50 TOLGrd 0.00001

! {  CHARMM MOLVIB Module is used for molecular vibrational calculation  }
! calculate (second derivatives)/(force constants in cartesian coor) and pass to MOLVIB

MOLVIB -
 NDI1 24 -
 NDI2 24 -
 NDI3 34 -
 SECO
GFX
PRNT 0
IC                              ! no.IC type   specify
    1    1    2    0    0       !  1    BOND    C1=N2
    1    2    3    0    0       !  2    BOND    N2-C3
    1    3    4    0    0       !  3    BOND    C3=C4
    1    4    5    0    0       !  4    BOND    C4-S5
    1    1    5    0    0       !  5    BOND    C1-S5
    1    1    6    0    0       !  6    BOND    C1-N6
    1    3    7    0    0       !  7    BOND    C3-H7
    1    4    8    0    0       !  8    BOND    C4-H8
    1    6    9    0    0       !  9    BOND    N6-H9
    1    6   10    0    0       ! 10    BOND    N6-H10
    2    5    1    2    0       ! 11    ANGL    S5-C1=N2        : 5 member ring a1
    2    1    2    3    0       ! 12    ANGL    C1=N2-C3                        a2
    2    2    3    4    0       ! 13    ANGL    N2-C3=C4                        a3
    2    3    4    5    0       ! 14    ANGL    C3=C4-S5                        a4
    2    4    5    1    0       ! 15    ANGL    C4-S5-C1                        a5
    4    5    1    2    3       ! 16    DIHE    S5-C1=N2-C3                     t1
    4    1    2    3    4       ! 17    DIHE    C1=N2-C3=C4                     t2
    4    2    3    4    5       ! 18    DIHE    N2-C3=C4-S5                     t3
    4    3    4    5    1       ! 19    DIHE    C3=C4-S5-C1                     t4
    4    4    5    1    2       ! 20    DIHE    C4-S5-C1=N2                     t5
    2    2    1    6    0       ! 21    ANGL    N2=C1-N6        : C1    b1
    2    5    1    6    0       ! 22    ANGL    S5-C1-N6                b2
    3    6    2    5    1       ! 23    WWAG    N6-C1(=N2)-S5           N6 wagging on N2-C1-S5 plane
    2    2    3    7    0       ! 24    ANGL    N2-C3-H7        : C3 b1
    2    4    3    7    0       ! 25    ANGL    C4=C3-H7             b2
    3    7    4    2    3       ! 26    WWAG    H7-C3(-N2)-C4        H7 wagging on N2-C3=C4 plane
    2    3    4    8    0       ! 27    ANGL    C3=C4-H8        : C4    b1
    2    5    4    8    0       ! 28    ANGL    S5-C4-H8                b2
    3    8    5    3    4       ! 29    WWAG    H8-C4(-S5)=C3           H8 wagging on S5-C4=C3 plane
    2    9    6   10    0       ! 30    ANGL    H9-N6-H10       : N6 a
    2    1    6    9    0       ! 31    ANGL    C1-N6-H9             b1
    2    1    6   10    0       ! 32    ANGL    C1-N6-H10            b2
    3    1    9   10    6       ! 33    WWAG    C1-N6(-H9)-H19       C1 wagging on NH2 plane
    4    2    1    6    9       ! 34    DIHE    N2-C1-N6-H9     : dihedral angles that makes H atoms NH2 rolling.
UMAT    0   1   0
  1  1  1.
  2  2  1.
  3  3  1.
  4  4  1.
  5  5  1.
  6  6  1.
  7  7  1.
  8  8  1.
  9  9  1.
 10 10  1.
 11 11  1.         11 12 -0.81       11 13  0.31       11 14  0.31       11 15 -0.81
 12 12 -1.12       12 13  1.81       12 14 -1.81       12 15  1.12
 13 16  0.31       13 17 -0.81       13 18  1.         13 19 -0.81       13 20  0.31
 14 16 -1.81       14 17 -1.12       14 19  1.12       14 20  1.81
 15 21  1.         15 22 -1.
 16 23  1.
 17 24  1.         17 25 -1.
 18 26  1.
 19 27  1.         19 28 -1.
 20 29  1.
 21 30  2.         21 31 -1.         21 32 -1.
 22 31  1.         22 32 -1.
 23 33  1.
 24 34  1.
-1
PED     0   15
  1  sC1=N2
  2  sN2-C3
  3  sC3=C4
  4  sC4-S5
  5  sC1-S5
  6  sC1-N6
  7  sC3-H7
  8  sC4-H8
  9  sN6-H9
 10  sN6-H10
 11  5rDef
 12  5rDef_
 13  5rTor
 14  5rTor_
 15  rocC1
 16  wagC1
 17  rocC3
 18  wagC3
 19  rocC4
 20  wagC4
 21  scsN6
 22  rocN6
 23  wagN6
 24  dNH2C1
-1
END

STOP